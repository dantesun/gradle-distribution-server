import groovyx.net.http.OkHttpBuilder
import org.jsoup.nodes.Document

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.github.http-builder-ng:http-builder-ng-okhttp:1.0.4'
        classpath 'org.jsoup:jsoup:1.9.2'
    }
}

apply plugin: 'base'

task fetchAllVersions() {
    outputs.upToDateWhen { false }
    doLast {
        logger.lifecycle("Get all Gradle distributions.")
        Document page = OkHttpBuilder.configure {
            request.uri = 'https://services.gradle.org/distributions/'
        }.get() as Document
        def versions = []
        page.select("a[href]").collect { it.attr('href') }.each { String href ->
            (href =~ /^\/distributions\/gradle-(.*)-(bin|all)\.zip$/).each {
                String version = it[1]
                if (version.contains("-rc-") || version.contains("-milestone-")) {
                    logger.info("Ignore RC/Milestone ${version}")
                } else {
                    versions << version
                }
            }
        }
        def version_file = file("${buildDir}/versions.txt")
        version_file.parentFile.mkdirs()
        version_file.text = versions.toSet().toSorted().join("\n") + "\n"
    }
}

